<!--
 * @author Alain Couthures <alain.couthures@agencexml.com>
 * @licence LGPL - See file 'LICENSE.md' in this project.
 * @module setfocus
-->
<xsl:template match="xforms:setfocus" mode="script" priority="1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:xforms="http://www.w3.org/2002/xforms">
	<xsl:param name="parentworkid"/>
	<xsl:param name="workid" select="concat(position(),'_',$parentworkid)"/>
	<xsl:apply-templates select="@*" mode="scriptattr"/>
	<js xmlns="">
		<xsl:text>var </xsl:text>
		<xsl:value-of select="$vn_pf"/>
		<xsl:text>setfocus_</xsl:text>
		<xsl:value-of select="$workid"/>
		<xsl:text> = new XsltForms_dispatch(</xsl:text>
		<xsl:value-of select="$vn_subform"/>
		<xsl:text>,"xforms-focus",</xsl:text>
		<xsl:choose>
			<xsl:when test="xforms:control/@value">
				<xsl:for-each select="xforms:control[1]">
					<xsl:call-template name="toScriptBinding"/>
				</xsl:for-each>
			</xsl:when>
			<xsl:otherwise>
				<xsl:variable name="control">
					<xsl:choose>
						<xsl:when test="xforms:control"><xsl:value-of select="xforms:control"/></xsl:when>
						<xsl:otherwise><xsl:value-of select="@control"/></xsl:otherwise>
					</xsl:choose>
				</xsl:variable>
				<xsl:call-template name="toScriptParam"><xsl:with-param name="p" select="$control"/></xsl:call-template>
			</xsl:otherwise>
		</xsl:choose>
		<xsl:text>,null,</xsl:text>
		<xsl:call-template name="toScriptParam"><xsl:with-param name="p" select="@if"/></xsl:call-template>
		<xsl:text>,</xsl:text>
		<xsl:call-template name="toScriptParam"><xsl:with-param name="p" select="@while"/></xsl:call-template>
		<xsl:text>,</xsl:text>
		<xsl:call-template name="toScriptParam"><xsl:with-param name="p" select="@iterate"/></xsl:call-template>
		<xsl:text>);</xsl:text>
	</js>
	<xsl:apply-templates select="node()" mode="script">
		<xsl:with-param name="parentworkid" select="$workid"/>
	</xsl:apply-templates>
</xsl:template>