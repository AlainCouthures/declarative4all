<!--
 * @author Alain Couthures <alain.couthures@agencexml.com>
 * @licence LGPL - See file 'LICENSE.md' in this project.
 * @module item
-->
<xsl:template match="xforms:item | xforms:itemset[ancestor::xforms:*[1][@appearance='full']]" mode="script" priority="1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:xforms="http://www.w3.org/2002/xforms">
	<xsl:param name="parentworkid"/>
	<xsl:param name="workid" select="concat(position(),'_',$parentworkid)"/>
	<xsl:apply-templates select="@*" mode="scriptattr"/>
	<js xmlns="">
		<xsl:variable name="lname" select="local-name()"/>
		<xsl:if test="local-name() = 'itemset'">
			<xsl:text>var </xsl:text>
			<xsl:value-of select="$vn_pf"/>
			<xsl:value-of select="$lname"/>
			<xsl:text>_</xsl:text>
			<xsl:value-of select="$workid"/>
			<xsl:text> = new XsltForms_repeat(</xsl:text>
			<xsl:value-of select="$vn_subform"/>
			<xsl:text>,</xsl:text>
			<xsl:choose>
				<xsl:when test="@id">"<xsl:value-of select="@id"/></xsl:when>
				<xsl:otherwise>
					<xsl:value-of select="$jsid_pf"/>
					<xsl:value-of select="$lname"/>
					<xsl:text>-</xsl:text>
					<xsl:value-of select="$workid"/>
				</xsl:otherwise>
			</xsl:choose>
			<xsl:text>",0,</xsl:text>
			<xsl:call-template name="toScriptBinding"><xsl:with-param name="p" select="@nodeset | @ref"/></xsl:call-template>
			<xsl:text>);
</xsl:text>
	</xsl:if>
		<xsl:text>var </xsl:text>
		<xsl:value-of select="$vn_pf"/>
		<xsl:value-of select="$lname"/>
		<xsl:text>_</xsl:text>
		<xsl:value-of select="$workid"/>
		<xsl:text> = new XsltForms_item(</xsl:text>
		<xsl:value-of select="$vn_subform"/>
		<xsl:text>,</xsl:text>
		<xsl:choose>
			<xsl:when test="@id">"<xsl:value-of select="@id"/></xsl:when>
			<xsl:otherwise>
				<xsl:value-of select="$jsid_pf"/>
				<xsl:value-of select="$lname"/>
				<xsl:text>-</xsl:text>
				<xsl:if test="local-name() = 'itemset'">
					<xsl:text>item-</xsl:text>
				</xsl:if>
				<xsl:value-of select="$workid"/>
			</xsl:otherwise>
		</xsl:choose>
		<xsl:text>",</xsl:text>
		<xsl:for-each select="xforms:label[1]">
			<xsl:call-template name="toScriptBinding"><xsl:with-param name="p" select="@ref"/></xsl:call-template>
		</xsl:for-each>
		<xsl:if test="xforms:value">
			<xsl:text>,</xsl:text>
			<xsl:for-each select="xforms:value[1]">
				<xsl:call-template name="toScriptBinding"><xsl:with-param name="p" select="@ref"/></xsl:call-template>
			</xsl:for-each>
		</xsl:if>
		<xsl:text>);</xsl:text>
	</js>
	<xsl:apply-templates select="node()" mode="script">
		<xsl:with-param name="parentworkid" select="$workid"/>
	</xsl:apply-templates>
	<xsl:call-template name="listeners">
		<xsl:with-param name="workid" select="$workid"/>
	</xsl:call-template>
</xsl:template>