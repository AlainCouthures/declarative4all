<!--
 * @author Alain Couthures <alain.couthures@agencexml.com>
 * @licence LGPL - See file 'LICENSE.md' in this project.
 * @module itemset
-->
<xsl:template match="xforms:itemset" mode="item" priority="2" xmlns:xhtml="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:xforms="http://www.w3.org/2002/xforms">
	<xsl:param name="type" select="false()"/> 
	<xsl:param name="parentworkid"/>
	<xsl:param name="workid" select="concat(position(),'_',$parentworkid)"/>
	<xsl:choose>
		<xsl:when test="$type">
			<div>
				<xsl:call-template name="genid">
					<xsl:with-param name="workid" select="$workid"/>
				</xsl:call-template>
				<xsl:call-template name="style">
					<xsl:with-param name="class">xforms-itemset</xsl:with-param>
				</xsl:call-template>
				<div class="xforms-item">
					<xsl:attribute name="id"><xsl:choose><xsl:when test="@id"><xsl:value-of select="@id"/></xsl:when><xsl:otherwise><xsl:value-of select="$id_pf"/>itemset-item-<xsl:value-of select="$workid"/></xsl:otherwise></xsl:choose></xsl:attribute>
					<xsl:variable name="inputid" select="concat($id_pf, 'input-', local-name(), '-', $workid)"/>
					<input type="{$type}" value="{xforms:value}" id="{$inputid}"/>
					<xsl:apply-templates select="node()">
						<xsl:with-param name="parentworkid" select="$workid"/>
						<xsl:with-param name="appearance">item</xsl:with-param>
						<xsl:with-param name="inputid" select="$inputid"/>
					</xsl:apply-templates>
				</div>
			</div>
		</xsl:when>
		<xsl:otherwise>
			<option class="xforms-disabled"><xsl:call-template name="genid"><xsl:with-param name="workid" select="$workid"/></xsl:call-template><xsl:value-of select="$nbsp"/></option>
		</xsl:otherwise>
	</xsl:choose>
</xsl:template>