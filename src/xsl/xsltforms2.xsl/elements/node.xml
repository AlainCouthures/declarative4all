<!--
 * @author Alain Couthures <alain.couthures@agencexml.com>
 * @licence LGPL - See file 'LICENSE.md' in this project.
 * @module node
-->
<xsl:template match="*" priority="-1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:choose>
		<xsl:when test="local-name() = 'instance' and namespace-uri() = 'http://www.w3.org/2002/xforms'">
			<xsl:element name="xforms-{local-name()}">
				<xsl:apply-templates select="@*"/>
				<script type="text/plain">
					<xsl:apply-templates select="node()" mode="xml2string"/>
				</script>
			</xsl:element>
		</xsl:when>
		<xsl:when test="local-name() = 'schema' and namespace-uri() = 'http://www.w3.org/2001/XMLSchema'">
			<script type="text/plain">
				<xsl:apply-templates select="." mode="xml2string"/>
			</script>
		</xsl:when>
		<xsl:when test="namespace-uri() = 'http://www.w3.org/2002/xforms'">
			<xsl:element name="xforms-{local-name()}">
				<xsl:apply-templates select="@*"/>
				<xsl:apply-templates select="node()"/>
			</xsl:element>
		</xsl:when>
		<xsl:when test="namespace-uri() != '' and namespace-uri() != 'http://www.w3.org/1999/xhtml'">
			<xsl:element name="{local-name()}" namespace="{namespace-uri()}">
				<xsl:apply-templates select="@*"/>
				<xsl:apply-templates select="node()"/>
			</xsl:element>
		</xsl:when>
		<xsl:otherwise>
			<xsl:element name="{local-name()}">
				<xsl:apply-templates select="@*"/>
				<xsl:apply-templates select="node()"/>
			</xsl:element>
		</xsl:otherwise>
	</xsl:choose>
</xsl:template>
<xsl:template match="@*" priority="-1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:attribute name="{local-name()}"><xsl:value-of select="."/></xsl:attribute>
</xsl:template>
<xsl:template match="processing-instruction()" priority="-1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"/>
<xsl:template match="text()[normalize-space(.)='']" priority="-1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"/>
<xsl:template match="node()" priority="-2" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:copy>
		<xsl:apply-templates select="node()"/>
	</xsl:copy>
</xsl:template>